---
layout: 12pt
header-includes:
   - \usepackage{lineno}
   - \linenumbers
   - \usepackage{setspace}
bibliography: ~/Dropbox/Bibliography/MicroMeso.bib
csl: components/ecology.csl

## rmarkdown render options
output:
  pdf_document:
    fig_caption: true
    keep_tex: true
fontsize: 12pt
geometry: margin=1in

---

\renewcommand\thefigure{A\arabic{figure}}  

Appendix A: Detailed model descriptions
=======================================
Here we describe the statistical models in complete detail, including full description of the Bayesian models and Stan code for each model. We write each model as three parts: (1) a data model (likelihood), (2) a process model, and (3) a parameter model. We then combine these three models to write the posterior and joint distributions as a single model statement. Following the full model expression we provide the Stan code that directly corresponds the statement of posterior and joint distributions. In our expressions of the posterior and joint distributions we use general probability notation where [.] represents some unspecified distribution. We do this for clarity of model presentation. All prior distributions are defined in the parameter model sections. For clarity we avoid subscripting species (the *j* subscripts) after initial description of each model.

Parameter to model notation keys
--------------------------------

Table:  Definition of parameters and model notation for IPM survival and growth models.

| Parameter | Definition | Notation in statistical results tables |
| --------- | ---------- | -------------------------------------- |
| $\gamma_{t}$ | intercept for year *t* | ``a[t]`` where ``t`` is the numeric year |
| $\tilde{\gamma}$ | global intercept | ``a_mu`` |
| $\phi_{Q}$ | random effect of quadrat group *Q* | ``gint[Q]`` where ``Q`` is the quadrat group |
| $\beta_{t}$ | size effect for year *t* | ``b1[t]`` where ``t`` is the numeric year |
| $\tilde{\beta}$ | global size effect | ``b1_mu`` |
| $\gamma$ | effect of crowding | ``w[1]`` |
| $\nu$ | crowding $\times$ size effect | ``w[2]`` |
| $\theta_{k}$ | effect of climate covariate *k* | ``b2[k]`` where ``k`` is the climate effect |
| *a* | first parameter for growth variance | ``tau`` |
| *b* | second parameter for growth variance | ``tauSize`` |

Table:  Definition of parameters and model notation for IPM recruitment model.

| Parameter | Definition | Notation in statistical results tables |
| --------- | ---------- | -------------------------------------- |
| $\gamma_{t}$ | intercept for year *t* | ``a[t]`` where ``t`` is the numeric year |
| $\tilde{\gamma}$ | global intercept | ``a_mu`` |
| $\phi_{Q}$ | random effect of quadrat group *Q* | ``gint[Q]`` where ``Q`` is the quadrat group |
| $\gamma$ | effect of plot cover | ``dd`` |
| $\theta_{k}$ | effect of climate covariate *k* | ``b2[k]`` where ``k`` is the climate effect |
| *p* | mixing fraction for effective plot cover | ``u`` |
| $\zeta$ | size parameter for negative binimial likelihood | ``theta`` |

Table:  Definition of parameters and model notation for quadrat based model.

| Parameter | Definition | Notation in statistical results tables |
| --------- | ---------- | -------------------------------------- |
| $\gamma_{t}$ | intercept for year *t* | ``a[t]`` where ``t`` is the numeric year |
| $\tilde{\gamma}$ | global intercept | ``a_mu`` |
| $\phi_{Q}$ | random effect of quadrat group *Q* | ``gint[Q]`` where ``Q`` is the quadrat group |
| $\beta_{t}$ | cover effect for year *t* | ``b1[t]`` where ``t`` is the numeric year |
| $\tilde{\beta}$ | global cover effect | ``b1_mu`` |
| $\theta_{k}$ | effect of climate covariate *k* | ``b2[k]`` where ``k`` is the climate effect |
| $\tau$ | model variance in log normal likelihood | ``tau`` |

Table: Climate effect key.

| Integer ID (*k*) | Climate covariate |
| ---------------- | ----------------- |
| 1 | pptLag |
| 2 | ppt1 |
| 3 | ppt2 |
| 4 | TmeanSpr1 |
| 5 | TmeanSpr2 |
| 6 | ppt1 $\times$ TmeanSpr1 |
| 7 | ppt2 $\times$ TmeanSpr2 |
| 8 | pptLag $\times$ size |
| 9 | ppt1 $\times$ size |
| 10 | ppt2 $\times$ size |
| 11 | TmeanSpr1 $\times$ size |
| 12 | TmeanSpr2 $\times$ size |

Survival (IPM)
--------------
We used logistic regression to model survival probability ($S$) of genet $i$ from species $j$ in quadrat group $Q$ from time $t$ to $t+1$:

\begin{align}
\text{logit}(S_{ijQ,t}) &= \gamma^{S}_{j,t} + \phi^{S}_{jQ} + \beta^{S}_{j,t}x_{ij,t} + \omega^{S}_{j}w_{ij,t} + \nu^{S}_{j}w_{ij,t}x_{ij,t} + \theta^{S}_{jk}C_{k,t} \\
y^{S}_{ijQ,t} &\sim \text{Bernoulli}(S_{ijQ,t})
\end{align}

where $x_{ij,t}$ is the log of genet size, $\gamma^{S}_{j,t}$ is a year-specific intercept, $\beta^{S}_{j,t}$ is the year-specific slope parameter for size, $\phi^{S}_{jQ}$ is the random effect of quadrat group location, and $\theta^{S}_{k}$ is the fixed parameter for the effect of the $k$th climate covariate at time $t$ ($C_{k,t}$). Note that the vector of climate covariates (**C**) includes climate variable interactions and climate$\times$size interactions. We include density-dependence by estimating the effect of crowding on the focal individual by other individuals of the same species. $\omega$ is the effect of crowding and $w_{t,Q}$ is the crowding experienced by the focal individual at time $t$ in quadrat group $Q$. We include a size$\times$crowding interaction effect ($\nu^{S}$).

**Data, process, and parameter models**

\begin{align}
&\mathtt{data} \qquad &\textbf{y} \sim \text{Bernoulli}(\boldsymbol{\mu}) \\
&\mathtt{process} \qquad &\text{logit}(\mu_{iQ,t}) = \gamma_{t} + \phi_{Q} + \beta_{t}x_{i,t} + \omega w_{i,t} + \nu w_{i,t}x_{i,t} + \theta_{k}C_{k,t} \\
&\mathtt{parameters} \qquad &\gamma_{t} \sim \text{Normal}(\tilde{\gamma}, \sigma_\gamma^2)\\
& \qquad & \phi_{Q} \sim \text{Normal}(0,\sigma_Q^2) \\
& \qquad & \beta_{t} \sim \text{Normal}(\tilde{\beta}, \sigma_\beta^2) \\
& \qquad & \omega \sim \text{Uniform}(-100, 100) \\
& \qquad & \nu \sim \text{Uniform}(-100, 100) \\
& \qquad & \boldsymbol{\theta} \sim \text{Uniform}(-10, 10) \\
& \qquad & \tilde{\gamma} \sim \text{Uniform}(-300, 300) \\
& \qquad & \tilde{\beta} \sim \text{Uniform}(-100, 100) \\
& \qquad & \sigma_Q^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \sigma_\gamma^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \sigma_\beta^2 \sim \text{Cauchy}(0, 5)
\end{align}

**Full expression of posterior and joint distributions**
\begin{align}
[\boldsymbol{\gamma}, \tilde{\gamma}, \boldsymbol{\phi}, \boldsymbol{\beta}, \tilde{\beta}, \omega, \boldsymbol{\theta}, \sigma_Q^2, \sigma_\beta^2] &\propto \\
&\prod_{t=1}^T \prod_{i=1}^n [y_{iQ,t} | \gamma_{t}, \phi_{Q}, \beta_{t}, \omega, \boldsymbol{\theta}] [\gamma_{t} | \tilde{\gamma}] [\beta_{t} | \tilde{\beta}, \sigma_\beta^2] \times \\
&\prod_{Q=1}^{Q_{tot}} [\phi_{Q} | \sigma_Q^2] \times \\
&[\boldsymbol{\theta}] [\omega] [\tilde{\gamma}] [\tilde{\beta}] [\sigma_Q^2] [\sigma_\beta^2]
\end{align}

**Stan code for model**
```{r survival_stan, echo=TRUE, eval=FALSE}
data{
  int<lower=0> N; // observations
  int<lower=0> Yrs; // years
  int<lower=0> yid[N]; // year id
  int<lower=0> Covs; // climate covariates
  int<lower=0> G; // groups
  int<lower=0> gid[N]; // group id
  int<lower=0,upper=1> Y[N]; // observation vector
  matrix[N,Covs] C; // climate matrix
  vector[N] X; // size vector
  matrix[N,2] W; // crowding matrix
}
parameters{
  real a_mu;
  vector[Yrs] a;
  real b1_mu;
  vector[Yrs] b1;
  vector[Covs] b2;
  vector[2] w;
  vector[G] gint;
  real<lower=0> sig_a;
  real<lower=0> sig_b1;
  real<lower=0> sig_G;
}
transformed parameters{
  real mu[N];
  vector[N] climEff;
  vector[N] crowdEff;
  climEff <- C*b2;
  crowdEff <- W*w;
  for(n in 1:N){
    mu[n] <- inv_logit(a[yid[n]] + gint[gid[n]] + b1[yid[n]]*X[n] + 
                         crowdEff[n] + climEff[n]);
  }
}
model{
  // Priors
  a_mu ~ uniform(-300,300);
  w ~ uniform(-100,100);
  b1_mu ~ uniform(-100,100);
  sig_a ~ cauchy(0,5);
  sig_b1 ~ cauchy(0,5);
  sig_G ~ cauchy(0,5);
  gint ~ normal(0, sig_G);
  b2 ~ uniform(-10,10);
  a ~ normal(a_mu, sig_a);
  b1 ~ normal(b1_mu, sig_b1);

  // Likelihood
  Y ~ binomial(1,mu);
}
```


Growth (IPM)
------------
We modeled growth as a Gaussian process describing genet size at time $t+1$ as a function of size at $t$ and climate covariates:

\begin{align}
x_{ijQ,t+1} &= \gamma^{G}_{j,t} + \phi^{G}_{jQ} + \beta^{G}_{j,t}x_{ij,t} + \omega^{G}_{j}w_{ij,t} + \nu^{S}_{j}w_{ij,t}x_{ij,t} + \theta^{G}_{jk}C_{k,t} \\
y^{G}_{ijQ,t} &\sim \text{Normal}(x_{ijQ,t+1}, \varepsilon_{ij,t})
\end{align}

where $x$ is log genet size and all other parameters are as described for the survival regression. We capture non-constant error variance in growth by modeling the variance around the growth regression ($\varepsilon$) as a nonlinear function of predicted genet size:

\begin{align}
\varepsilon_{ij,t} = a e^{b x_{ijQ,t+1}}
\end{align}

**Data, process, and parameter models.**
\begin{align}
&\mathtt{data} \qquad &\textbf{y} \sim \text{Normal}(\boldsymbol{\mu}, \boldsymbol{\sigma}^2) \\
&\mathtt{process} \qquad &\mu_{iQ,t+1} = \gamma_{t} + \phi_{Q} + \beta_{t}x_{i,t} + \omega w_{i,t} + \nu w_{i,t}x_{i,t} + \theta_{k}C_{k,t} \\
& \qquad & \sigma^2_{iQ,t} = ae^{b\mu_{iQ,t+1}} \\
&\mathtt{parameters} \qquad &\gamma_{t} \sim \text{Normal}(\tilde{\gamma}, \sigma_\gamma^2)\\
& \qquad & \phi_{Q} \sim \text{Normal}(0,\sigma_Q^2) \\
& \qquad & \beta_{t} \sim \text{Normal}(\tilde{\beta}, \sigma_\beta^2) \\
& \qquad & \omega \sim \text{Normal}(0, 0.001) \\
& \qquad & \nu \sim \text{Normal}(0, 0.001) \\
& \qquad & \boldsymbol{\theta} \sim \text{Normal}(0, 0.001) \\
& \qquad & \tilde{\gamma} \sim \text{Normal}(0, 0.001) \\
& \qquad & \tilde{\beta} \sim \text{Normal}(0, 0.001) \\
& \qquad & a \sim \text{Normal}(0, 0.001) \\
& \qquad & b \sim \text{Normal}(0, 0.001) \\
& \qquad & \sigma_Q^2 \sim \text{Uniform}(0, 1000) \\
& \qquad & \sigma_\gamma^2 \sim \text{Uniform}(0, 1000) \\
& \qquad & \sigma_\beta^2 \sim \text{Uniform}(0, 1000)
\end{align}

**Full expression of posterior and joint distributions**
\begin{align}
[\boldsymbol{\gamma}, \tilde{\gamma}, \boldsymbol{\phi}, \boldsymbol{\beta}, \tilde{\beta}, \omega, \nu, \boldsymbol{\theta}, a, b, \sigma_Q^2, \sigma_\beta^2] &\propto \\
&\prod_{t=1}^T \prod_{i=1}^n [y_{iQ,t} | \gamma_{t}, \phi_{Q}, \beta_{t}, \omega, \nu, \boldsymbol{\theta}, a, b] [\gamma_{t} | \tilde{\gamma}] [\beta_{t} | \tilde{\beta}, \sigma_\beta^2] \times \\
&\prod_{Q=1}^{Q_{tot}} [\phi_{Q} | \sigma_Q^2] \times \\
&[\boldsymbol{\theta}] [\omega] [\nu] [\tilde{\gamma}] [\tilde{\beta}] [\sigma_Q^2] [\sigma_\beta^2] [a] [b]
\end{align}

**Stan code for model**
```{r grow_stan, echo=TRUE, eval=FALSE}
data{
  int<lower=0> N; // observations
  int<lower=0> Yrs; // years
  int<lower=0> yid[N]; // year id
  int<lower=0> Covs; // climate covariates
  int<lower=0> G; // groups
  int<lower=0> gid[N]; // group id
  vector[N] Y; // observation vector
  matrix[N,Covs] C; // climate matrix
  vector[N] X; // size vector
  matrix[N,2] W; // crowding matrix
}
parameters{
  real a_mu;
  vector[Yrs] a;
  real b1_mu;
  vector[Yrs] b1;
  vector[Covs] b2;
  vector[2] w;
  real gint[G];
  real tau;
  real tauSize;
  real<lower=0> sig_a;
  real<lower=0> sig_b1;
  real<lower=0> sig_G;
}
transformed parameters{
  real mu[N];
  real<lower=0> sigma[N];
  vector[N] climEff;
  vector[N] crowdEff;
  climEff <- C*b2;
  crowdEff <- W*w;
  for(n in 1:N){
    mu[n] <- a[yid[n]] + gint[gid[n]] + b1[yid[n]]*X[n] + crowdEff[n] + climEff[n];
    sigma[n] <- sqrt((fmax(tau*exp(tauSize*mu[n]), 0.0000001)));  
  }
}
model{
  // Priors
  a_mu ~ normal(0,1000);
  w ~ normal(0,1000);
  b1_mu ~ normal(0,1000);
  tau ~ normal(0,1000);
  tauSize ~ normal(0,1000);
  sig_a ~ uniform(0,1000);
  sig_b1 ~ uniform(0,1000);
  sig_G ~ uniform(0,1000);
  for(g in 1:G)
      gint[g] ~ normal(0, sig_G);
  for(c in 1:Covs)
    b2[c] ~ normal(0,1000);
  for(y in 1:Yrs){
    a[y] ~ normal(a_mu, sig_a);
    b1[y] ~ normal(b1_mu, sig_b1);
  }

  // Likelihood
  Y ~ normal(mu, sigma);
}
```


Recruitment (IPM)
-----------------
Our data allows us to track new recruits, but we cannot assign a specific parent to new genets. So, for recruitment, we work at the quadrat level and model the number of new individuals of species $j$ in quadrat $q$ recruiting at time $t+1$ as a function of quadrat "effective cover" ($A'$) in the previous year ($t$). Effective cover is a mixture of observed cover ($A$) in the focal quadrat ($q$) and the mean cover across the entire group ($\bar{A}$) of $Q$ quadrats in which $q$ is located:

\begin{equation}
A'_{jq,t} = p_{j}A_{jq,t} + (1-p_{j})\bar{A}_{jQ,t}
\end{equation}

where $p$ is a mixing fraction between 0 and 1 that is estimated within the model.

We assume the number of individuals, $y^{R}$, recruiting at time $t+1$ follows a negative binomial distribution:

\begin{equation}
y^{R}_{jq,t+1} \sim \text{NegBin}(\lambda_{jq,t+1},\zeta)
\end{equation}

where $\lambda$ is the mean intensity and $\zeta$ is the size parameter. We define $\lambda$ as:

\begin{equation}
\lambda_{jq,t+1} = A'_{jq,t}e^{(\gamma^{R}_{j,t} + \phi^{R}_{jQ} + \theta^{R}_{jk}C_{k,t} + \omega^{R}\sqrt{A'_{q,t}})}
\end{equation}

where $A'$ is effective cover ($\text{cm}^2$) of species $j$ in quadrat $q$ and all other terms are as in the survival and growth regressions.

**Data, process, and parameter models**
\begin{align}
&\mathtt{data} \qquad &\textbf{y} \sim \text{NegBin}(\boldsymbol{\lambda},\zeta) \\
&\mathtt{process} \qquad &\lambda_{jq,t+1} = A'_{q,t}e^{(\gamma_{t} + \phi_{Q} + \theta_{k}C_{k,t} + \omega\sqrt{A'_{q,t}})} \\
&\mathtt{parameters} \qquad &\gamma_{t} \sim \text{Normal}(\tilde{\gamma}, \sigma_\gamma^2)\\
& \qquad & \phi_{Q} \sim \text{Normal}(0,\sigma_Q^2) \\
& \qquad & \omega \sim \text{Uniform}(-100, 100) \\
& \qquad & \boldsymbol{\theta} \sim \text{Uniform}(-10, 10) \\
& \qquad & \tilde{\gamma} \sim \text{Uniform}(-300, 300) \\
& \qquad & \sigma_Q^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \sigma_\gamma^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \zeta \sim \text{Uniform}(0, 100) \\
& \qquad & u \sim \text{Uniform}(0, 1)
\end{align}

**Full expression of posterior and joint distributions**
\begin{align}
[\boldsymbol{\gamma}, \tilde{\gamma}, \boldsymbol{\phi}, \omega, \boldsymbol{\theta}, \sigma_Q^2, \zeta, u] &\propto \\
&\prod_{t=1}^T \prod_{i=1}^n [y_{iQ,t} | \gamma_{t}, \phi_{Q}, \omega, \boldsymbol{\theta}, \zeta, u] [\gamma_{t} | \tilde{\gamma}] \times \\
&\prod_{Q=1}^{Q_{tot}} [\phi_{Q} | \sigma_Q^2] [\boldsymbol{\theta}] [\omega] [\tilde{\gamma}] [\sigma_Q^2] [\zeta] [u]
\end{align}

**Stan code for model**
```{r rec_stan, echo=TRUE, eval=FALSE}
data{
  int<lower=0> N; // observations
  int<lower=0> Yrs; // years
  int<lower=0> yid[N]; // year id
  int<lower=0> Covs; // climate covariates
  int<lower=0> G; // groups
  int<lower=0> gid[N]; // group id
  int<lower=0> Y[N]; // observation vector
  matrix[N,Covs] C; // climate matrix
  vector[N] parents1; // crowding vector
  vector[N] parents2; // crowding vector
}
parameters{
  real a_mu;
  vector[Yrs] a;
  vector[Covs] b2;
  real dd;
  real gint[G];
  real<lower=0> sig_a;
  real<lower=0> theta;
  real<lower=0> sig_G;
  real<lower=0, upper=1> u;
}
transformed parameters{
  real mu[N];
  vector[N] climEff;
  vector[N] trueP1;
  vector[N] trueP2;
  vector[N] lambda;
  vector[N] q;
  climEff <- C*b2;
  for(n in 1:N){
    trueP1[n] <- parents1[n]*u + parents2[n]*(1-u);
    trueP2[n] <- sqrt(trueP1[n]);
    mu[n] <- exp(a[yid[n]] + gint[gid[n]] + dd*trueP2[n] + climEff[n]);
    lambda[n] <- trueP1[n]*mu[n];
    q[n] <- lambda[n]*theta;
  }
}
model{
  // Priors
  u ~ uniform(0,1);
  theta ~ uniform(0,100);
  a_mu ~ normal(0,1000);
  dd ~ uniform(-100,100);
  sig_a ~ cauchy(0,5);
  sig_G ~ cauchy(0,5);
  for(g in 1:G)
      gint[g] ~ normal(0, sig_G);
  for(c in 1:Covs)
    b2 ~ uniform(-100,100);
  for(y in 1:Yrs){
    a[y] ~ normal(a_mu, sig_a);
  }

  // Likelihood
  Y ~ neg_binomial_2(q, theta);
}
```


Quadrat based model (QBM)
-------------------------
The model for quadrat cover change from time $t$ to $t+1$ is

\begin{align}
x_{jq,t+1} &= \gamma^{P}_{j,t} + \phi^{P}_{jQ} + \beta^{P}_{j,t}x_{jq,t} + \theta^{P}_{jk}C_{k,t} \\
y^{P}_{jq,t+1} &\sim \text{LogNormal}(x_{jq,t+1}, \tau_{j}) \text{T}[0,1]
\end{align}

where $x_{jq,t}$ is the log of species' $j$ proportional cover in quadrat $q$ at time $t$ and all other parameters are as in the individual-level growth model (Eq. 3). Again, note that the climate covariate vector (**C**) includes the climate$\times$cover interaction. The log normal likelihood includes a truncation (T[0,1]) to ensure that predicted values do not exceed 100% cover. 

**Data, process, and parameter models.**
\begin{align}
&\mathtt{data} \qquad &\textbf{y} \sim \text{LogNormal}(\boldsymbol{\mu}, \boldsymbol{\tau}) \text{T}[0,1] \\
&\mathtt{process} \qquad &\mu_{iQ,t+1} = \gamma_{t} + \phi_{Q} + \beta_{t}x_{i,t} + \theta_{k}C_{k,t} \\
&\mathtt{parameters} \qquad &\gamma_{t} \sim \text{Normal}(\tilde{\gamma}, \sigma_\gamma^2)\\
& \qquad & \phi_{Q} \sim \text{Normal}(0,\sigma_Q^2) \\
& \qquad & \beta_{t} \sim \text{Normal}(\tilde{\beta}, \sigma_\beta^2) \\
& \qquad & \boldsymbol{\theta} \sim \text{Uniform}(-10, 10) \\
& \qquad & \tilde{\gamma} \sim \text{Uniform}(-300, 300) \\
& \qquad & \tilde{\beta} \sim \text{Uniform}(-100, 100) \\
& \qquad & \sigma_Q^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \sigma_\gamma^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \sigma_\beta^2 \sim \text{Cauchy}(0, 5) \\
& \qquad & \tau \sim \text{Cauchy}(0, 5)
\end{align}

**Full expression of posterior and joint distributions**
\begin{align}
[\boldsymbol{\gamma}, \tilde{\gamma}, \boldsymbol{\phi}, \boldsymbol{\beta}, \tilde{\beta}, \boldsymbol{\theta}, \sigma_Q^2, \sigma_\beta^2, \tau] &\propto \\
&\prod_{t=1}^T \prod_{i=1}^n [y_{iQ,t} | \gamma_{t}, \phi_{Q}, \beta_{t}, \boldsymbol{\theta}, \tau] [\gamma_{t} | \tilde{\gamma}] [\beta_{t} | \tilde{\beta}, \sigma_\beta^2] \times \\
&\prod_{Q=1}^{Q_{tot}} [\phi_{Q} | \sigma_Q^2] \times \\
&[\boldsymbol{\theta}] [\omega] [\tilde{\gamma}] [\tilde{\beta}] [\sigma_Q^2] [\sigma_\beta^2] [\tau]
\end{align}

**Stan code for model**
```{r qbm_stan, echo=TRUE, eval=FALSE}
data{
  int<lower=0> N; // observations
  int<lower=0> Yrs; // years
  int<lower=0> yid[N]; // year id
  int<lower=0> Covs; // climate covariates
  int<lower=0> G; // groups
  int<lower=0> gid[N]; // group id
  real<lower=0,upper=1> Y[N]; // observation vector
  matrix[N,Covs] C; // climate matrix
  vector[N] X; // size vector
}
parameters{
  real a_mu;
  vector[Yrs] a;
  real b1_mu;
  vector[Yrs] b1;
  vector[Covs] b2;
  vector[G] gint;
  real<lower=0> sig_a;
  real<lower=0> sig_b1;
  real<lower=0> sig_G;
  real<lower=0> tau;
}
transformed parameters{
  real mu[N];
  vector[N] climEff;
  climEff <- C*b2;
  for(n in 1:N)
    mu[n] <- a[yid[n]] + gint[gid[n]] + b1[yid[n]]*X[n] + climEff[n];
}
model{
  // Priors
  a_mu ~ uniform(-300,300);
  b1_mu ~ uniform(-100,100);
  sig_a ~ cauchy(0,5);
  sig_b1 ~ cauchy(0,5);
  sig_G ~ cauchy(0,5);
  gint ~ normal(0, sig_G);
  b2 ~ uniform(-10,10);
  a ~ normal(a_mu, sig_a);
  b1 ~ normal(b1_mu, sig_b1);
  tau ~ cauchy(0,5);

  //Likelihood
  Y ~ lognormal(mu, tau);
}
```
